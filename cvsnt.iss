; Script generated by the My Inno Setup Extensions Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppName=cvsnt
AppVerName=cvsnt
AppPublisherURL=http://www.cvsnt.org
AppSupportURL=http://www.cvsnt.org
AppUpdatesURL=http://www.cvsnt.org
DefaultDirName={pf}\cvsnt
DefaultGroupName=CVSNT
AllowNoIcons=true
LicenseFile=c:\cvsnt-sjis\cvsnt-2.0.14\COPYING
DisableStartupPrompt=true
PrivilegesRequired=none
UninstallDisplayIcon={app}\cvs.exe
UninstallDisplayName=CVSNT
OutputDir=c:\cvsnt-sjis\bin
Compression=bzip
FlatComponentsList=false
OutputBaseFilename=cvsnt_sjis_install
AppID=CVSNT
MinVersion=4.0.1111,4.0.1381sp6
ShowTasksTreeLines=false
SourceDir=c:\cvsnt-sjis\bin
SolidCompression=true
AllowUNCPath=false
ShowLanguageDialog=auto

UseSetupLdr=true
[_ISTool]
EnableISX=true
UseAbsolutePaths=false
Use7zip=false

[LangOptions]
LanguageID=$0809

[Types]
Name: typical; Description: 標準インストール
Name: full; Description: フルインストール
Name: custom; Description: カスタムインストール; Flags: iscustom

[Components]
Name: Commandline; Description: コマンドラインクライアント; Types: custom typical full
Name: Server; Description: サーバー コンポーネント; Types: custom full typical; MinVersion: 0,4.00.1381sp6; Check: isadmin
Name: win95; Description: Windows 95 サポート; Types: custom full
Name: RCS; Description: RCS エミュレーション コンポーネント; Types: custom full typical; MinVersion: 0,4.0.1381sp6
Name: Protocols; Description: プロトコル; Types: custom typical full
Name: Protocols\Pserver; Description: パスワードサーバー (:pserver:) プロトコル; Types: custom typical full
Name: Protocols\NTServer; Description: 名前つきパイプ (:ntserver:) プロトコル; Types: custom full; MinVersion: 0,4.00.1381sp6
Name: Protocols\Ext; Description: 外部コマンド (:ext:) プロトコル; Types: custom typical full
Name: Protocols\Fork; Description: Fork (:fork:) プロトコル (for testing only)
Name: Protocols\GSSAPI\gssapi_ad; Description: GSSAPI (:gserver:) Active Directory用; Types: custom typical full; MinVersion: 0,5.00.2195
Name: Protocols\GSSAPI\gssapi_mit; Description: GSSAPI (:gserver:) MIT Kerberos用; Types: custom full
Name: Protocols\SSPI; Description: SSPI (:sspi:) プロトコル; Types: typical full custom
Name: Protocols\server; Description: RSH クライアント (:server:) プロトコル; Types: custom full typical
Name: Protocols\ssh; Description: SSH クライアント (:ssh:) プロトコル; Types: custom full typical
Name: Protocols\sserver; Description: SSL (:sserver) プロトコル; Types: custom full typical
Name: Protocols\sserver\CA; Description: 共通 CA 証明書; Types: custom full typical
Name: Development; Description: プラグイン開発; Types: custom full
Name: readme; Description: Readme とヘルプ; Types: custom typical full

[Files]
Source: cvs.exe; DestDir: {app}; Flags: ignoreversion regtypelib; Components: Commandline; MinVersion: 0,4.00.1381sp6
Source: cvs95.exe; DestDir: {app}; Flags: ignoreversion; Components: win95
Source: cvslock.exe; DestDir: {app}; Flags: ignoreversion; MinVersion: 0,4.00.1381sp6; Components: Server; Check: StopCvsLockService
Source: cvsnt.cpl; DestDir: {sys}; Flags: ignoreversion; Components: Server; MinVersion: 0,4.00.1381sp6
Source: cvsservice.exe; DestDir: {app}; Flags: ignoreversion; MinVersion: 0,4.00.1381sp6; Components: Server; Check: StopCvsService
Source: ext_protocol.dll; DestDir: {app}; Flags: ignoreversion; Components: Protocols\Ext
Source: fork_protocol.dll; DestDir: {app}; Flags: ignoreversion; Components: Protocols\Fork
Source: gserver_protocol.dll; DestDir: {app}; Flags: ignoreversion; Components: Protocols\GSSAPI\gssapi_ad
Source: ntserver_protocol.dll; DestDir: {app}; Flags: ignoreversion; Components: Protocols\NTServer
Source: postinst.exe; DestDir: {app}; Flags: ignoreversion; Components: win95 Server Commandline
Source: uninsthlp.exe; DestDir: {app}; Flags: ignoreversion; Components: win95 Server Commandline
Source: protocol_map.ini; DestDir: {app}; Flags: ignoreversion; Components: Commandline Protocols\server Protocols\SSPI Protocols\GSSAPI\gssapi_mit Protocols\GSSAPI\gssapi_ad Protocols\Fork Protocols\Ext Protocols\NTServer
Source: pserver_protocol.dll; DestDir: {app}; Flags: ignoreversion; Components: Protocols\Pserver
Source: server_protocol.dll; DestDir: {app}; Flags: ignoreversion; Components: Protocols\server
Source: sspi_protocol.dll; DestDir: {app}; Flags: ignoreversion; Components: Protocols\SSPI
Source: gserver_protocol_mit.dll; DestDir: {app}\mit; Components: Protocols\GSSAPI\gssapi_mit; Flags: ignoreversion
Source: gssapi32.dll; DestDir: {app}\mit; Components: Protocols\GSSAPI\gssapi_mit; Flags: ignoreversion
Source: krb5_32.dll; DestDir: {app}\mit; Components: Protocols\GSSAPI\gssapi_mit; Flags: ignoreversion
Source: comerr32.dll; DestDir: {app}\mit; Components: Protocols\GSSAPI\gssapi_mit; Flags: ignoreversion
Source: sysfiles\msvcr71.dll; DestDir: {sys}; Flags: uninsneveruninstall sharedfile; Check: isadmin
Source: sysfiles\msvcp71.dll; DestDir: {sys}; Flags: uninsneveruninstall sharedfile; Check: isadmin
Source: sysfiles\mfc71.dll; DestDir: {sys}; Flags: uninsneveruninstall sharedfile; Check: isadmin
Source: sysfiles\mfc71u.dll; DestDir: {sys}; Flags: uninsneveruninstall sharedfile; Check: isadmin
Source: sysfiles\msvcp71.dll; DestDir: {app}
Source: sysfiles\msvcr71.dll; DestDir: {app}
Source: sysfiles\mfc71.dll; DestDir: {app}
Source: sysfiles\mfc71u.dll; DestDir: {app}
Source: ssh_protocol.dll; DestDir: {app}; Flags: ignoreversion; Components: Protocols\ssh
Source: plink.dll; DestDir: {app}; Flags: ignoreversion; Components: Protocols\ssh
Source: sysfiles\dbghelp.dll; DestDir: {app}
Source: sysfiles\ssleay32_vc71.dll; DestDir: {app}; Flags: ignoreversion; Components: Protocols\sserver
Source: sysfiles\libeay32_vc71.dll; DestDir: {app}; Flags: ignoreversion; Components: Protocols\sserver
Source: sserver_protocol.dll; DestDir: {app}; Flags: ignoreversion; Components: Protocols\sserver
Source: genkey.exe; DestDir: {app}; Flags: ignoreversion; Components: Protocols\sserver
Source: rcsdiff.exe; DestDir: {app}; Flags: ignoreversion; MinVersion: 0,4.00.1381sp6; Components: RCS
Source: co.exe; DestDir: {app}; Flags: ignoreversion; MinVersion: 0,4.00.1381sp6; Components: RCS
Source: rlog.exe; DestDir: {app}; Flags: ignoreversion; MinVersion: 0,4.00.1381sp6; Components: RCS
Source: ca.pem; DestDir: {app}; Flags: ignoreversion; Components: Protocols\sserver\CA
Source: COPYING; DestDir: {app}
Source: infolib.h; DestDir: {app}; Flags: ignoreversion; Components: Development

[INI]
Filename: {app}\cvs.url; Section: InternetShortcut; Key: URL; String: http://www.cvsnt.org; Components: readme
Filename: {app}\readme.url; Section: InternetShortcut; Key: URL; String: http://www.cvsnt.org/wiki/ReadMe; Components: readme
Filename: {app}\cvs-command.url; Section: InternetShortcut; Key: URL; String: http://www.cvsnt.org/wiki/CvsCommand; Components: readme
Filename: {app}\cvs-reference.url; Section: InternetShortcut; Key: URL; String: http://www.cvsnt.org/wiki/CvsIndex; Components: readme

[Icons]
Name: {group}\Readme; Filename: {app}\readme.url; Flags: createonlyiffileexists; Components: readme
Name: {group}\Service control panel; Filename: {sys}\cvsnt.cpl; IconIndex: 0; Flags: createonlyiffileexists; MinVersion: 0,4.00.1381sp6; Components: Server; IconFilename: {app}\cvs.exe
Name: {group}\cvsnt home page; Filename: {app}\cvs.url; Components: readme
Name: {group}\CVSNT command reference; Filename: {app}\cvs-command.url; Flags: createonlyiffileexists; Components: readme
Name: {group}\CVSNT online documentation; Filename: {app}\cvs-reference.url; Flags: createonlyiffileexists; Components: readme
Name: {group}\Uninstall cvsnt; Filename: {uninstallexe}

[UninstallDelete]
Name: {app}\cvs.url; Type: files
Name: {app}\cvsnt-default.pem; Type: files
Name: {app}\readme.url; Type: files
Name: {app}\cvs-command.url; Type: files
Name: {app}\cvs-reference.url; Type: files

[Run]
Filename: {sys}\net.exe; Parameters: stop cvslock; Components: Server; Tasks: lockservice; Flags: runhidden; WorkingDir: {app}
Filename: {sys}\net.exe; Parameters: stop cvs; Tasks: service; Components: Server; Flags: runhidden; WorkingDir: {app}
Filename: {app}\cvslock.exe; Parameters: -u; Components: Server; Tasks: lockservice; Flags: runhidden; WorkingDir: {app}
Filename: {app}\cvsservice.exe; Parameters: -u; Components: Server; Tasks: service; Flags: runhidden; WorkingDir: {app}
Filename: {app}\cvsservice.exe; Parameters: -i; Components: Server; Tasks: service; WorkingDir: {app}; Description: CVS サービスをインストールしています; StatusMsg: CVS サービスをインストールしています; Flags: runhidden
Filename: {app}\cvslock.exe; Parameters: -i; Components: Server; Tasks: lockservice; WorkingDir: {app}; Description: CVS ロックサービスをインストールしています; StatusMsg: CVS ロックサービスをインストールしています; Flags: runhidden
Filename: {sys}\net.exe; Parameters: start cvs; Tasks: service; Components: Server; WorkingDir: {app}; Description: CVS サービスを開始する; StatusMsg: CVS サービスを開始しています; Flags: runhidden postinstall nowait
Filename: {sys}\net.exe; Parameters: start cvslock; Components: Server; Tasks: lockservice; WorkingDir: {app}; Description: CVS ロックサービスを開始する; StatusMsg: CVS ロックサービスを開始しています; Flags: runhidden postinstall nowait
Filename: {app}\genkey.exe; Parameters: cvsnt-default.pem; WorkingDir: {app}; Description: デフォルトサーバー証明書を生成しています; StatusMsg: デフォルトサーバー証明書を生成しています; Flags: runhidden; Components: Protocols\sserver; Tasks: genkey

[Dirs]

[Tasks]
Name: service; Description: cvsnt サービスのインストール; MinVersion: 0,4.00.1381sp6; Components: Server; Check: isadmin
Name: lockservice; Description: ロックサービスのインストール; Components: Server; MinVersion: 0,4.00.1381sp6; Check: isadmin
Name: genkey; Description: デフォルト証明書を生成する; Components: Protocols\sserver

[UninstallRun]
Filename: {sys}\net.exe; Parameters: stop cvslock; Components: Server; Tasks: lockservice; Flags: runhidden; WorkingDir: {app}
Filename: {sys}\net.exe; Parameters: stop cvs; Tasks: service; Components: Server; Flags: runhidden; WorkingDir: {app}
Filename: {app}\cvslock.exe; Parameters: -u; Components: Server; Tasks: lockservice; Flags: runhidden; WorkingDir: {app}
Filename: {app}\cvsservice.exe; Parameters: -u; Components: Server; Tasks: service; Flags: runhidden; WorkingDir: {app}
Filename: {app}\uninsthlp.exe; Parameters: {app}; Components: win95 Server Commandline; Flags: runhidden; WorkingDir: {app}

[Registry]
Root: HKLM; Subkey: Software\CVS\Pserver; ValueType: expandsz; ValueName: CertificateFile; ValueData: {app}\cvsnt-default.pem; Flags: createvalueifdoesntexist preservestringtype; Components: Server
Root: HKLM; Subkey: Software\CVS\Pserver; ValueType: expandsz; ValueName: PrivateKeyFile; ValueData: {app}\cvsnt-default.pem; Flags: createvalueifdoesntexist preservestringtype; Components: Server
Root: HKLM; Subkey: Software\CVS\Pserver; ValueType: string; ValueName: InstallPath; ValueData: {app}; Flags: uninsdeletevalue
Root: HKCU; Subkey: Software\cvsnt\sserver; ValueType: dword; ValueName: StrictChecking; ValueData: 0; Flags: createvalueifdoesntexist; Components: Protocols\sserver
Root: HKCU; Subkey: Environment; ValueType: expandsz; ValueName: Path; Flags: createvalueifdoesntexist preservestringtype
[Code]
var
  Install : string;

procedure CurStepChanged(CurStep: integer);
var rslt : integer;
var path : string;
var mypath : string;
begin

	if IsAdminLoggedOn and (CurStep = csCopy) then
	begin
		if IsAdminLoggedOn then
		begin
			RegQueryStringValue(HKEY_LOCAL_MACHINE,'SYSTEM\CurrentControlSet\Control\Session Manager\Environment','Path', path);
			mypath := ExpandConstant('{app}');
			rslt := Pos(mypath,path);
			if rslt = 0 then
			begin
				mypath := GetShortName(mypath);
				rslt := Pos(mypath,path);
				if rslt = 0 then
				begin
				    path := path + ';' + mypath;
					RegWriteStringValue(HKEY_LOCAL_MACHINE,'SYSTEM\CurrentControlSet\Control\Session Manager\Environment','Path', path);
				end
			end
		end
		else
		begin
			RegQueryStringValue(HKEY_CURRENT_USER,'Environment','Path', path);
			mypath := ExpandConstant('{app}');
			rslt := Pos(mypath,path);
			if rslt = 0 then
			begin
				mypath := GetShortName(mypath);
				rslt := Pos(mypath,path);
				if rslt = 0 then
				begin
				    path := path + ';' + mypath;
			    	RegWriteStringValue(HKEY_CURRENT_USER,'Environment','Path', path);
				end
			end
		end

		// WM_WININICHANGE == WM_SETTINGCHANGE == 0x001A
		// No way of specifying string (LongInt(PChar('Environment')) does't work)
		// so specify 0 for 'generic'
		SendBroadcastMessage($001a, 0, 0);

	end
end;

function isadmin : boolean;
begin
  Result := IsAdminLoggedOn;
end;

function StopCvsService : Boolean;
var rslt : integer;
begin
	InstExec(GetSystemDir()+'\net.exe','stop cvs',GetSystemDir(),true,true,0,rslt);
	InstExec(GetSystemDir()+'\net.exe','stop cvsssh',GetSystemDir(),true,true,0,rslt);
	Result := True;
end;

function StopCvsLockService : Boolean;
var rslt : integer;
begin
	InstExec(GetSystemDir()+'\net.exe','stop cvslock',GetSystemDir(),true,true,0,rslt);
	Result := True;
end;

function ScriptDlgPages(CurPage: Integer; BackClicked: Boolean):
Boolean;
var
  Next: Boolean;
begin
  if (not BackClicked and (CurPage = wpSelectDir)) or (BackClicked and (CurPage = wpSelectComponents)) then begin
    ScriptDlgPageSetCaption('アンチウィルス 警告');
    ScriptDlgPageSetSubCaption1('');
    Result := OutputMsg(
    'もし、アンチウィルス ソフトウェアを使用している場合、サーバーが動作しているコンピュータ上で' +
    '「ファイルシステムリアルタイム保護」を無効にしてください。' + #13#10#13#10 +
    'クライアント側でもこのオプションを有効にしていると問題があるかもしれません。', true) xor BackClicked;
    ScriptDlgPageClose(not Result);
  end
  else
     Result := not BackClicked;
end;

function NextButtonClick(CurPage: Integer): Boolean;
begin
  Result := ScriptDlgPages(CurPage, False);
end;

function BackButtonClick(CurPage: Integer): Boolean;
begin
  Result := ScriptDlgPages(CurPage, True);
end;






